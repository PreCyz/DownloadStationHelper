group 'download-station-helper'
version '1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.8
mainClassName = 'pg.Main'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'

}

def copyRunScript = copySpec {
    from 'src/main/resources/distro'
    include '*.sh'
}

def copyApplicationProperties = copySpec {
    from 'src/main/resources/distro'
    include '*.properties','credentials'
}

task copyAppPropertiesTask( type: Copy ) {
    into "$buildDir/libs/settings"
    with copyApplicationProperties
}

task copyRunScripTask( type: Copy ) {
    into "$buildDir/libs"
    with copyRunScript
}

//create a single Jar with all dependencies
task oneJar(type: Jar) {
    dependsOn copyAppPropertiesTask, copyRunScripTask
    manifest {
        attributes 'Implementation-Title': 'DownloadStationHelper',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyToLib( type: Copy ) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}

jar {
    dependsOn copyToLib, copyAppPropertiesTask, copyRunScripTask
    manifest {
        attributes 'Implementation-Title': 'DownloadStationHelper',
                'Implementation-Version': version,
                'Main-Class': mainClassName,
                'Class-path': configurations.compile.collect { 'lib/' + it.getName() }.join(' ')
    }
    baseName = project.name + '-core'
}